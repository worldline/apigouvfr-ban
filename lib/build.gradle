plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.10'
    id 'org.jlleitschuh.gradle.ktlint' version "10.2.0"
    id 'org.jetbrains.dokka' version "1.6.0"
    id 'maven-publish'
}

apply from: '../jacoco.gradle'

android {
    compileSdk project.sdk_target

    defaultConfig {
        minSdk project.sdk_min
        targetSdk project.sdk_target
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xexplicit-api=strict'
    }
}

dependencies {
    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.2"

    // Ktor client
    implementation "io.ktor:ktor-client-core:2.0.1"
    implementation "io.ktor:ktor-client-cio:2.0.1"
    implementation "io.ktor:ktor-client-content-negotiation:2.0.1"
    implementation "io.ktor:ktor-serialization-kotlinx-json:2.0.1"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    testImplementation "io.ktor:ktor-client-mock:2.0.1"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.2"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.worldline.apigouvfr'
            artifactId = 'apigouvfr-ban-android'
            version = project.version_name

            pom {
                name = 'Base Adresse Nationale Android library'
                description = 'Android library for accessing Base Adresse Nationale (https://adresse.data.gouv.fr/api-doc/adresse)'
                url = 'https://github.com/worldline/apigouvfr-ban/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'bapness'
                        name = 'Baptiste CARLIER'
                        email = 'baptiste.carlier@worldline.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/worldline/apigouvfr-ban.git'
                    developerConnection = 'scm:git:ssh://github.com/worldline/apigouvfr-ban.git'
                    url = 'http://github.com/worldline/apigouvfr-ban/'
                }
                // A slightly hacky fix so that the POM will include transitive dependencies
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    project.configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}